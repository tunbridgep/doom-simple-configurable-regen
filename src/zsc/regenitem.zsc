class SimpleRegenerator : inventory
{
	default
    {
		inventory.maxamount 1;
	}

    int previousHealth;
    int currentTicks;
    int displayTicks;
    bool startedRegen;

    void PlayConditionalSound(int soundlevel, string sound, int channel, float volume, bool stopsound = false)
    {
        if (regen_sounds >= soundlevel)
        {
            if (stopsound)
                owner.A_StopSound(channel);
            owner.A_StartSound(sound, channel, CHANF_UI|CHANF_OVERLAP, volume, 0, 0);
        }
    }

    void DoNoMercy()
    {
        let dawn = Dawn(owner);
        if (regen_no_mercy && dawn)
            dawn.mercyCooldown = dawn.MERCY_DAMAGE_COOLDOWN;
    }

    void DoDangerState()
    {
        let dawn = Dawn(owner);
        int health = owner.Health;

        if (dawn)
        {
            if (dawn.isDangerState && health > regen_danger_health)
                dawn.EndDangerState();
            else if (!dawn.isDangerState && health <= regen_danger_health)
            {
                owner.GiveInventory("HasLowHealth", 1);
                dawn.StartDangerState();
            }
        }

    }

    override void PostBeginPlay()
    {
        Super.PostBeginPlay();
        Console.printf("Adding Regenerator");
    }

    //Set up the circular red bar that appears when regeneration is recharging
    void ResetChargeBar(int start_delay)
    { 

        //prevent division by zero
        if (start_delay <= 0)
            return;

        class<Inventory> itemClass = 'RegenTics';
        let item = owner.FindInventory(itemClass,true);

        if (!item)
        {
            owner.GiveInventory(itemClass.GetClassName(),9999);
            item = owner.FindInventory(itemClass,true);
        }

        if (item)
        {
            //We need to set our amount based on our percentage of the max
            let max = item.MaxAmount;

            float percent = (currentTicks + 0.0) / (start_delay + 0.0);
            //console.printf("currentTicks: %d, start_delay: %d, currentTicks / start_delay: %f",currentTicks,start_delay,percent);
            //console.printf("Max: %d", max * percent);
            //console.printf("Actual Max: %d",item.MaxAmount);
            let amt = item.MaxAmount - max * percent;
            item.Amount = int(amt + 0.5);
            //console.printf("New Amount: %d",item.Amount);
        }
    }

    void DoRegen()
    {
        if (!startedRegen)
        {
            startedRegen = true;
            owner.GiveInventory("RegenActive", 1);
        
            //play the start sound
            PlayConditionalSound(1,"player/regen/active", CHAN_UI, 0.35);
        }
            
        //Play healing sound
        PlayConditionalSound(2,"player/regen/tick", CHAN_5, 0.7);
           
        //Display graphics
        UpdateHealIndicator();

        //Heal
        int heal = owner.health + regen_per_tick;
        if (heal > regen_max)
            heal = regen_max;

        owner.A_SetHealth(heal);

        //if we're finished, stop regenerating
        if (heal >= regen_max)
            StopRegen();
    
        currentTicks = 0;
    }

    void StopRegen()
    {
        if (startedRegen)
        {
            owner.TakeInventory("RegenActive", 1);
            PlayConditionalSound(1,"player/regen/complete", CHAN_AUTO, 0.7, true);
            startedRegen = false;
        }
        currentTicks = 0;
    }


    void UpdateHealIndicator()
    {
        Dawn(owner).HealTick(10);
    }

    override void DoEffect()
    {
        
        Super.DoEffect();
        if (owner)
        {
            //We need to convert our start delay and delay per tick from seconds to tics
            int regen_delay_start_tics = regen_delay_start * 35.0;
            int regen_delay_per_tick_tics = regen_delay_per_tick * 35.0;
            int health = owner.Health;
            if (health <= 0)
                return;

            if (owner.CheckInventory("HardcoreMode",1) && !regen_allow_hardcore) //no regeneration on hardcore (and no post on sundays)
                return;

            if (level.IsFrozen()) //No change at all if the game is frozen
                return;

            //Stop regeneration if we're above max health
            if (health >= regen_max)
            {
                owner.TakeInventory("HasLowHealth", 1);
                StopRegen();
                owner.GiveInventory("RegenTics",9999);
            }

            //If we took damage last frame, reset the timer
            else if (health < previousHealth)
                StopRegen();

            //Otherwise, we can regenerate
            else if (health < regen_max)
            {
                //Update the health circle
                if (!startedRegen)
                    ResetChargeBar(regen_delay_start_tics);

                //Check for regen
                if (currentTicks >= regen_delay_start_tics && !startedRegen)
                    DoRegen();
                else if (currentTicks >= regen_delay_per_tick_tics && startedRegen)
                    DoRegen();
            }

            //Handle danger state (red vignette and cries of pain)
            DoDangerState();

            //If we've removed mercy, set it to always be on cooldown
            DoNoMercy();

            previousHealth = owner.Health;
            currentTicks++;
        }
    }
}
